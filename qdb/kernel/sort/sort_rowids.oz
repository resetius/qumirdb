(block
  (pragma language overloads)

  ;; Row-id sort helpers. Row ids are packed as (batch << 32) | row, matching
  ;; the native TRowId helpers. The sort kernels below work directly over the
  ;; caller-owned row-id permutation and read key values from TRowSet columns.

  (fun sr_batch_index ((var row_id i64)) -> i64
    (block
      (return (>> row_id (: 32 i64)))))

  (fun sr_row_index ((var row_id i64)) -> i64
    (block
      (return (& row_id (: 4294967295 i64)))))

  (fun sr_column
       ((var store <ptr TRowSet>) (var row_id i64) (var column_idx i64)) -> TColumn
    (block
      (var batch = (index store (call sr_batch_index row_id)))
      (var columns = (field batch Columns))
      (return (index columns column_idx))))

  (fun sr_row_valid
       ((var store <ptr TRowSet>) (var row_id i64) (var column_idx i64)) -> bool
    (block
      (var col = (call sr_column store row_id column_idx))
      (if (== (cast (field col Mask) i64) (: 0 i64))
        (block (return #t)))
      (return (call bitoff
        (field col Mask)
        (call sr_row_index row_id)
        (cast (field col MaskBitOffset) i64)))))

  (fun sr_load_fixed_key [Value]
       ((var store <ptr TRowSet>)
        (var row_id i64)
        (var column_idx i64)
        (var type_witness <ptr Value>)) -> Value
    (block
      (var col = (call sr_column store row_id column_idx))
      (var data = (cast (cast (field col Data) i64) <ptr Value>))
      (return (index data (call sr_row_index row_id)))))

  (fun sr_load_bool_key
       ((var store <ptr TRowSet>) (var row_id i64) (var column_idx i64)) -> u8
    (block
      (var col = (call sr_column store row_id column_idx))
      (if (call bitoff
            (cast (cast (field col Data) i64) <ptr u8>)
            (call sr_row_index row_id)
            (cast (field col DataBitOffset) i64))
        (block (return (: 1 u8))))
      (return (: 0 u8))))

  (fun sort_fixed_rowids_count_pass [Value]
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var row_ids <ptr i64>)
        (var work <ptr i64>)
        (var counts <ptr u32>)
        (var n i64)
        (var digit i64)
        (var desc bool)
        (var type_witness <ptr Value>))
    (block
      (var i i64)
      (= i (: 0 i64))
      (while (< i (: 256 i64))
        (block
          (= counts [i] (: 0 u32))
          (= i (+ i (: 1 i64)))))

      (= i (: 0 i64))
      (while (< i n)
        (block
          (var row = (index row_ids i))
          (var bucket = (call qumir_radix_digit
            (call qumir_radix_key
              (call sr_load_fixed_key store row column_idx type_witness))
            digit))
          (if desc
            (block (= bucket (- (: 255 u32) bucket))))
          (var bucket_index = (cast bucket i64))
          (= counts [bucket_index] (+ (index counts bucket_index) (: 1 u32)))
          (= i (+ i (: 1 i64)))))

      (= i (: 1 i64))
      (while (< i (: 256 i64))
        (block
          (= counts [i] (+ (index counts i) (index counts (- i (: 1 i64)))))
          (= i (+ i (: 1 i64)))))

      (= i n)
      (while (> i (: 0 i64))
        (block
          (= i (- i (: 1 i64)))
          (var row = (index row_ids i))
          (var bucket = (call qumir_radix_digit
            (call qumir_radix_key
              (call sr_load_fixed_key store row column_idx type_witness))
            digit))
          (if desc
            (block (= bucket (- (: 255 u32) bucket))))
          (var bucket_index = (cast bucket i64))
          (var place = (- (index counts bucket_index) (: 1 u32)))
          (= work [(cast place i64)] row)
          (= counts [bucket_index] place)))

      (= i (: 0 i64))
      (while (< i n)
        (block
          (= row_ids [i] (index work i))
          (= i (+ i (: 1 i64)))))))

  (fun sort_fixed_rowids [Value]
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var row_ids <ptr i64>)
        (var work <ptr i64>)
        (var counts <ptr u32>)
        (var n i64)
        (var key_bits i64)
        (var desc bool)
        (var type_witness <ptr Value>))
    (block
      (var digit i64)
      (= digit (: 0 i64))
      (while (< digit key_bits)
        (block
          (call sort_fixed_rowids_count_pass
            store column_idx row_ids work counts n digit desc type_witness)
          (= digit (+ digit (: 8 i64)))))))

  (fun sort_fixed_rowids_count_pass_nullable [Value]
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var row_ids <ptr i64>)
        (var work <ptr i64>)
        (var counts <ptr u32>)
        (var n i64)
        (var digit i64)
        (var desc bool)
        (var nulls_first bool)
        (var type_witness <ptr Value>))
    (block
      (var i i64)
      (= i (: 0 i64))
      (while (< i (: 257 i64))
        (block
          (= counts [i] (: 0 u32))
          (= i (+ i (: 1 i64)))))

      (= i (: 0 i64))
      (while (< i n)
        (block
          (var row = (index row_ids i))
          (var bucket i64)
          (if (! (call sr_row_valid store row column_idx))
            (block
              (if nulls_first
                (block (= bucket (: 0 i64)))
                (block (= bucket (: 256 i64)))))
            (block
              (var digit_bucket = (cast (call qumir_radix_digit
                (call qumir_radix_key
                  (call sr_load_fixed_key store row column_idx type_witness))
                digit) i64))
              (if desc
                (block (= digit_bucket (- (: 255 i64) digit_bucket))))
              (if nulls_first
                (block (= bucket (+ digit_bucket (: 1 i64))))
                (block (= bucket digit_bucket)))))
          (= counts [bucket] (+ (index counts bucket) (: 1 u32)))
          (= i (+ i (: 1 i64)))))

      (= i (: 1 i64))
      (while (< i (: 257 i64))
        (block
          (= counts [i] (+ (index counts i) (index counts (- i (: 1 i64)))))
          (= i (+ i (: 1 i64)))))

      (= i n)
      (while (> i (: 0 i64))
        (block
          (= i (- i (: 1 i64)))
          (var row = (index row_ids i))
          (var bucket i64)
          (if (! (call sr_row_valid store row column_idx))
            (block
              (if nulls_first
                (block (= bucket (: 0 i64)))
                (block (= bucket (: 256 i64)))))
            (block
              (var digit_bucket = (cast (call qumir_radix_digit
                (call qumir_radix_key
                  (call sr_load_fixed_key store row column_idx type_witness))
                digit) i64))
              (if desc
                (block (= digit_bucket (- (: 255 i64) digit_bucket))))
              (if nulls_first
                (block (= bucket (+ digit_bucket (: 1 i64))))
                (block (= bucket digit_bucket)))))
          (var place = (- (index counts bucket) (: 1 u32)))
          (= work [(cast place i64)] row)
          (= counts [bucket] place)))

      (= i (: 0 i64))
      (while (< i n)
        (block
          (= row_ids [i] (index work i))
          (= i (+ i (: 1 i64)))))))

  (fun sort_fixed_rowids_nullable [Value]
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var row_ids <ptr i64>)
        (var work <ptr i64>)
        (var counts <ptr u32>)
        (var n i64)
        (var key_bits i64)
        (var desc bool)
        (var nulls_first bool)
        (var type_witness <ptr Value>))
    (block
      (var digit i64)
      (= digit (: 0 i64))
      (while (< digit key_bits)
        (block
          (call sort_fixed_rowids_count_pass_nullable
            store column_idx row_ids work counts n digit desc nulls_first type_witness)
          (= digit (+ digit (: 8 i64)))))))

  ;; BinInt (128-bit decimal) radix key for one 64-bit half. A signed 128-bit value
  ;; orders as (Hi signed, Lo unsigned): flip Hi's sign bit, leave Lo as-is. Radix is
  ;; stable + LSD, so sorting by Lo first and Hi last makes Hi the primary key.
  (fun sr_binint_radix_key
       ((var store <ptr TRowSet>)
        (var row_id i64)
        (var column_idx i64)
        (var use_hi bool)) -> u64
    (block
      (var witness = (cast (: 0 i64) <ptr BinInt>))
      (var v = (call sr_load_fixed_key store row_id column_idx witness))
      (if use_hi
        (block (return (^ (field v Hi) (<< (: 1 u64) (: 63 u64))))))
      (return (field v Lo))))

  (fun sort_binint_rowids_count_pass
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var row_ids <ptr i64>)
        (var work <ptr i64>)
        (var counts <ptr u32>)
        (var n i64)
        (var digit i64)
        (var desc bool)
        (var use_hi bool))
    (block
      (var i i64)
      (= i (: 0 i64))
      (while (< i (: 256 i64))
        (block
          (= counts [i] (: 0 u32))
          (= i (+ i (: 1 i64)))))

      (= i (: 0 i64))
      (while (< i n)
        (block
          (var row = (index row_ids i))
          (var bucket = (call qumir_radix_digit
            (call sr_binint_radix_key store row column_idx use_hi) digit))
          (if desc
            (block (= bucket (- (: 255 u32) bucket))))
          (var bucket_index = (cast bucket i64))
          (= counts [bucket_index] (+ (index counts bucket_index) (: 1 u32)))
          (= i (+ i (: 1 i64)))))

      (= i (: 1 i64))
      (while (< i (: 256 i64))
        (block
          (= counts [i] (+ (index counts i) (index counts (- i (: 1 i64)))))
          (= i (+ i (: 1 i64)))))

      (= i n)
      (while (> i (: 0 i64))
        (block
          (= i (- i (: 1 i64)))
          (var row = (index row_ids i))
          (var bucket = (call qumir_radix_digit
            (call sr_binint_radix_key store row column_idx use_hi) digit))
          (if desc
            (block (= bucket (- (: 255 u32) bucket))))
          (var bucket_index = (cast bucket i64))
          (var place = (- (index counts bucket_index) (: 1 u32)))
          (= work [(cast place i64)] row)
          (= counts [bucket_index] place)))

      (= i (: 0 i64))
      (while (< i n)
        (block
          (= row_ids [i] (index work i))
          (= i (+ i (: 1 i64)))))))

  (fun sort_binint_rowids
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var row_ids <ptr i64>)
        (var work <ptr i64>)
        (var counts <ptr u32>)
        (var n i64)
        (var desc bool))
    (block
      (var digit i64)
      (= digit (: 0 i64))
      (while (< digit (: 64 i64))
        (block
          (call sort_binint_rowids_count_pass
            store column_idx row_ids work counts n digit desc #f)
          (= digit (+ digit (: 8 i64)))))
      (= digit (: 0 i64))
      (while (< digit (: 64 i64))
        (block
          (call sort_binint_rowids_count_pass
            store column_idx row_ids work counts n digit desc #t)
          (= digit (+ digit (: 8 i64)))))))

  (fun sort_binint_rowids_count_pass_nullable
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var row_ids <ptr i64>)
        (var work <ptr i64>)
        (var counts <ptr u32>)
        (var n i64)
        (var digit i64)
        (var desc bool)
        (var nulls_first bool)
        (var use_hi bool))
    (block
      (var i i64)
      (= i (: 0 i64))
      (while (< i (: 257 i64))
        (block
          (= counts [i] (: 0 u32))
          (= i (+ i (: 1 i64)))))

      (= i (: 0 i64))
      (while (< i n)
        (block
          (var row = (index row_ids i))
          (var bucket i64)
          (if (! (call sr_row_valid store row column_idx))
            (block
              (if nulls_first
                (block (= bucket (: 0 i64)))
                (block (= bucket (: 256 i64)))))
            (block
              (var digit_bucket = (cast (call qumir_radix_digit
                (call sr_binint_radix_key store row column_idx use_hi) digit) i64))
              (if desc
                (block (= digit_bucket (- (: 255 i64) digit_bucket))))
              (if nulls_first
                (block (= bucket (+ digit_bucket (: 1 i64))))
                (block (= bucket digit_bucket)))))
          (= counts [bucket] (+ (index counts bucket) (: 1 u32)))
          (= i (+ i (: 1 i64)))))

      (= i (: 1 i64))
      (while (< i (: 257 i64))
        (block
          (= counts [i] (+ (index counts i) (index counts (- i (: 1 i64)))))
          (= i (+ i (: 1 i64)))))

      (= i n)
      (while (> i (: 0 i64))
        (block
          (= i (- i (: 1 i64)))
          (var row = (index row_ids i))
          (var bucket i64)
          (if (! (call sr_row_valid store row column_idx))
            (block
              (if nulls_first
                (block (= bucket (: 0 i64)))
                (block (= bucket (: 256 i64)))))
            (block
              (var digit_bucket = (cast (call qumir_radix_digit
                (call sr_binint_radix_key store row column_idx use_hi) digit) i64))
              (if desc
                (block (= digit_bucket (- (: 255 i64) digit_bucket))))
              (if nulls_first
                (block (= bucket (+ digit_bucket (: 1 i64))))
                (block (= bucket digit_bucket)))))
          (var place = (- (index counts bucket) (: 1 u32)))
          (= work [(cast place i64)] row)
          (= counts [bucket] place)))

      (= i (: 0 i64))
      (while (< i n)
        (block
          (= row_ids [i] (index work i))
          (= i (+ i (: 1 i64)))))))

  (fun sort_binint_rowids_nullable
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var row_ids <ptr i64>)
        (var work <ptr i64>)
        (var counts <ptr u32>)
        (var n i64)
        (var desc bool)
        (var nulls_first bool))
    (block
      (var digit i64)
      (= digit (: 0 i64))
      (while (< digit (: 64 i64))
        (block
          (call sort_binint_rowids_count_pass_nullable
            store column_idx row_ids work counts n digit desc nulls_first #f)
          (= digit (+ digit (: 8 i64)))))
      (= digit (: 0 i64))
      (while (< digit (: 64 i64))
        (block
          (call sort_binint_rowids_count_pass_nullable
            store column_idx row_ids work counts n digit desc nulls_first #t)
          (= digit (+ digit (: 8 i64)))))))

  (fun sort_bool_rowids_count_pass
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var row_ids <ptr i64>)
        (var work <ptr i64>)
        (var counts <ptr u32>)
        (var n i64)
        (var desc bool))
    (block
      (var i i64)
      (= i (: 0 i64))
      (while (< i (: 256 i64))
        (block
          (= counts [i] (: 0 u32))
          (= i (+ i (: 1 i64)))))

      (= i (: 0 i64))
      (while (< i n)
        (block
          (var row = (index row_ids i))
          (var bucket = (cast (call sr_load_bool_key store row column_idx) u32))
          (if desc
            (block (= bucket (- (: 255 u32) bucket))))
          (var bucket_index = (cast bucket i64))
          (= counts [bucket_index] (+ (index counts bucket_index) (: 1 u32)))
          (= i (+ i (: 1 i64)))))

      (= i (: 1 i64))
      (while (< i (: 256 i64))
        (block
          (= counts [i] (+ (index counts i) (index counts (- i (: 1 i64)))))
          (= i (+ i (: 1 i64)))))

      (= i n)
      (while (> i (: 0 i64))
        (block
          (= i (- i (: 1 i64)))
          (var row = (index row_ids i))
          (var bucket = (cast (call sr_load_bool_key store row column_idx) u32))
          (if desc
            (block (= bucket (- (: 255 u32) bucket))))
          (var bucket_index = (cast bucket i64))
          (var place = (- (index counts bucket_index) (: 1 u32)))
          (= work [(cast place i64)] row)
          (= counts [bucket_index] place)))

      (= i (: 0 i64))
      (while (< i n)
        (block
          (= row_ids [i] (index work i))
          (= i (+ i (: 1 i64)))))))

  (fun sort_bool_rowids
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var row_ids <ptr i64>)
        (var work <ptr i64>)
        (var counts <ptr u32>)
        (var n i64)
        (var desc bool))
    (block
      (call sort_bool_rowids_count_pass store column_idx row_ids work counts n desc)))

  (fun sort_bool_rowids_count_pass_nullable
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var row_ids <ptr i64>)
        (var work <ptr i64>)
        (var counts <ptr u32>)
        (var n i64)
        (var desc bool)
        (var nulls_first bool))
    (block
      (var i i64)
      (= i (: 0 i64))
      (while (< i (: 257 i64))
        (block
          (= counts [i] (: 0 u32))
          (= i (+ i (: 1 i64)))))

      (= i (: 0 i64))
      (while (< i n)
        (block
          (var row = (index row_ids i))
          (var bucket i64)
          (if (! (call sr_row_valid store row column_idx))
            (block
              (if nulls_first
                (block (= bucket (: 0 i64)))
                (block (= bucket (: 256 i64)))))
            (block
              (var digit_bucket = (cast (call sr_load_bool_key store row column_idx) i64))
              (if desc
                (block (= digit_bucket (- (: 255 i64) digit_bucket))))
              (if nulls_first
                (block (= bucket (+ digit_bucket (: 1 i64))))
                (block (= bucket digit_bucket)))))
          (= counts [bucket] (+ (index counts bucket) (: 1 u32)))
          (= i (+ i (: 1 i64)))))

      (= i (: 1 i64))
      (while (< i (: 257 i64))
        (block
          (= counts [i] (+ (index counts i) (index counts (- i (: 1 i64)))))
          (= i (+ i (: 1 i64)))))

      (= i n)
      (while (> i (: 0 i64))
        (block
          (= i (- i (: 1 i64)))
          (var row = (index row_ids i))
          (var bucket i64)
          (if (! (call sr_row_valid store row column_idx))
            (block
              (if nulls_first
                (block (= bucket (: 0 i64)))
                (block (= bucket (: 256 i64)))))
            (block
              (var digit_bucket = (cast (call sr_load_bool_key store row column_idx) i64))
              (if desc
                (block (= digit_bucket (- (: 255 i64) digit_bucket))))
              (if nulls_first
                (block (= bucket (+ digit_bucket (: 1 i64))))
                (block (= bucket digit_bucket)))))
          (var place = (- (index counts bucket) (: 1 u32)))
          (= work [(cast place i64)] row)
          (= counts [bucket] place)))

      (= i (: 0 i64))
      (while (< i n)
        (block
          (= row_ids [i] (index work i))
          (= i (+ i (: 1 i64)))))))

  (fun sort_bool_rowids_nullable
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var row_ids <ptr i64>)
        (var work <ptr i64>)
        (var counts <ptr u32>)
        (var n i64)
        (var desc bool)
        (var nulls_first bool))
    (block
      (call sort_bool_rowids_count_pass_nullable
        store column_idx row_ids work counts n desc nulls_first)))

  (fun sr_col_offset ((var col TColumn) (var i i64)) -> i64
    (block
      (if (== (cast (field col OffsetWidth) i64) (: 4 i64))
        (block
          (var offsets32 = (cast (cast (field col Offsets) i64) <ptr i32>))
          (return (cast (index offsets32 i) i64))))
      (var offsets64 = (cast (cast (field col Offsets) i64) <ptr i64>))
      (return (index offsets64 i))))

  (fun sr_col_begin ((var col TColumn) (var row i64)) -> i64
    (block
      (return (- (call sr_col_offset col row)
                 (call sr_col_offset col (: 0 i64))))))

  (fun sr_col_size ((var col TColumn) (var row i64)) -> i64
    (block
      (return (- (call sr_col_offset col (+ row (: 1 i64)))
                 (call sr_col_offset col row)))))

  ;; First 8 bytes as a big-endian u64; short strings are zero-padded.
  (fun sr_make_prefix
       ((var store <ptr TRowSet>) (var column_idx i64) (var row_id i64)) -> u64
    (block
      (var col = (call sr_column store row_id column_idx))
      (var data = (cast (cast (field col Data) i64) <ptr u8>))
      (var row = (call sr_row_index row_id))
      (var begin = (call sr_col_begin col row))
      (var size = (call sr_col_size col row))
      (var limit = size)
      (if (> limit (: 8 i64))
        (block (= limit (: 8 i64))))
      (var prefix = (: 0 u64))
      (var i i64)
      (= i (: 0 i64))
      (while (< i limit)
        (block
          (= prefix (| (<< prefix (: 8 u64))
                       (cast (index data (+ begin i)) u64)))
          (= i (+ i (: 1 i64)))))
      (= prefix (<< prefix (cast (* (: 8 i64) (- (: 8 i64) limit)) u64)))
      (return prefix)))

  ;; memcmp + length tie-break over row ids: -1 / 0 / 1.
  (fun sr_sort_compare
       ((var store <ptr TRowSet>) (var column_idx i64) (var a i64) (var b i64)) -> i64
    (block
      (var a_col = (call sr_column store a column_idx))
      (var b_col = (call sr_column store b column_idx))
      (var a_data = (cast (cast (field a_col Data) i64) <ptr u8>))
      (var b_data = (cast (cast (field b_col Data) i64) <ptr u8>))
      (var a_row = (call sr_row_index a))
      (var b_row = (call sr_row_index b))
      (var a_begin = (call sr_col_begin a_col a_row))
      (var b_begin = (call sr_col_begin b_col b_row))
      (var a_size = (call sr_col_size a_col a_row))
      (var b_size = (call sr_col_size b_col b_row))
      (var limit = a_size)
      (if (< b_size limit)
        (block (= limit b_size)))
      (var i i64)
      (= i (: 0 i64))
      (while (< i limit)
        (block
          (var a_byte = (cast (index a_data (+ a_begin i)) i64))
          (var b_byte = (cast (index b_data (+ b_begin i)) i64))
          (if (< a_byte b_byte)
            (block (return (: -1 i64))))
          (if (> a_byte b_byte)
            (block (return (: 1 i64))))
          (= i (+ i (: 1 i64)))))
      (if (< a_size b_size)
        (block (return (: -1 i64))))
      (if (> a_size b_size)
        (block (return (: 1 i64))))
      (return (: 0 i64))))

  ;; True when the pair at `a` must stay before/at the pair at `b`
  ;; (stable keep-order test over full strings).
  (fun sr_pair_keep_order
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var pairs <ptr u64>)
        (var a i64)
        (var b i64)
        (var desc bool)) -> bool
    (block
      (var row_a = (cast (index pairs (+ (* a (: 2 i64)) (: 1 i64))) i64))
      (var row_b = (cast (index pairs (+ (* b (: 2 i64)) (: 1 i64))) i64))
      (var cmp = (call sr_sort_compare store column_idx row_a row_b))
      (if desc
        (block (return (>= cmp (: 0 i64)))))
      (return (<= cmp (: 0 i64)))))

  ;; One stable counting pass over prefix byte `digit` (bits) of the pairs.
  (fun sr_pair_count_pass
       ((var pairs <ptr u64>)
        (var work <ptr u64>)
        (var counts <ptr u32>)
        (var n i64)
        (var digit i64)
        (var desc bool))
    (block
      (var i i64)
      (= i (: 0 i64))
      (while (< i (: 256 i64))
        (block
          (= counts [i] (: 0 u32))
          (= i (+ i (: 1 i64)))))

      (= i (: 0 i64))
      (while (< i n)
        (block
          (var bucket = (cast (& (>> (index pairs (* i (: 2 i64)))
                                     (cast digit u64))
                                 (: 255 u64)) i64))
          (if desc
            (block (= bucket (- (: 255 i64) bucket))))
          (= counts [bucket] (+ (index counts bucket) (: 1 u32)))
          (= i (+ i (: 1 i64)))))

      (= i (: 1 i64))
      (while (< i (: 256 i64))
        (block
          (= counts [i] (+ (index counts i) (index counts (- i (: 1 i64)))))
          (= i (+ i (: 1 i64)))))

      (= i n)
      (while (> i (: 0 i64))
        (block
          (= i (- i (: 1 i64)))
          (var bucket = (cast (& (>> (index pairs (* i (: 2 i64)))
                                     (cast digit u64))
                                 (: 255 u64)) i64))
          (if desc
            (block (= bucket (- (: 255 i64) bucket))))
          (var place = (cast (- (index counts bucket) (: 1 u32)) i64))
          (= work [(* place (: 2 i64))] (index pairs (* i (: 2 i64))))
          (= work [(+ (* place (: 2 i64)) (: 1 i64))]
             (index pairs (+ (* i (: 2 i64)) (: 1 i64))))
          (= counts [bucket] (cast place u32))))

      (= i (: 0 i64))
      (while (< i (* n (: 2 i64)))
        (block
          (= pairs [i] (index work i))
          (= i (+ i (: 1 i64)))))))

  ;; True when the row at pair position `j` must move past `key_row`.
  (fun sr_pair_shifts
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var pairs <ptr u64>)
        (var j i64)
        (var key_row i64)
        (var desc bool)) -> bool
    (block
      (var j_row = (cast (index pairs (+ (* j (: 2 i64)) (: 1 i64))) i64))
      (var cmp = (call sr_sort_compare store column_idx j_row key_row))
      (if desc
        (block (return (< cmp (: 0 i64)))))
      (return (> cmp (: 0 i64)))))

  ;; Stable insertion sort of pairs[lo, hi) by full string compare.
  (fun sr_pair_insertion
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var pairs <ptr u64>)
        (var lo i64)
        (var hi i64)
        (var desc bool))
    (block
      (var i = (+ lo (: 1 i64)))
      (while (< i hi)
        (block
          (var key_prefix = (index pairs (* i (: 2 i64))))
          (var key_row = (index pairs (+ (* i (: 2 i64)) (: 1 i64))))
          (var j = (- i (: 1 i64)))
          (var moving = #t)
          (while (&& (>= j lo) moving)
            (block
              (if (call sr_pair_shifts store column_idx
                    pairs j (cast key_row i64) desc)
                (block
                  (= pairs [(* (+ j (: 1 i64)) (: 2 i64))]
                     (index pairs (* j (: 2 i64))))
                  (= pairs [(+ (* (+ j (: 1 i64)) (: 2 i64)) (: 1 i64))]
                     (index pairs (+ (* j (: 2 i64)) (: 1 i64))))
                  (= j (- j (: 1 i64))))
                (block
                  (= moving #f)))))
          (= pairs [(* (+ j (: 1 i64)) (: 2 i64))] key_prefix)
          (= pairs [(+ (* (+ j (: 1 i64)) (: 2 i64)) (: 1 i64))] key_row)
          (= i (+ i (: 1 i64)))))))

  ;; Stable bottom-up merge sort of pairs[lo, hi); `work` holds pairs too.
  (fun sr_pair_merge_range
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var pairs <ptr u64>)
        (var work <ptr u64>)
        (var lo i64)
        (var hi i64)
        (var desc bool))
    (block
      (if (<= (- hi lo) (: 32 i64))
        (block
          (call sr_pair_insertion store column_idx pairs lo hi desc)
          (return)))

      (var block_lo = lo)
      (while (< block_lo hi)
        (block
          (var block_hi = (+ block_lo (: 32 i64)))
          (if (> block_hi hi)
            (block (= block_hi hi)))
          (call sr_pair_insertion store column_idx pairs block_lo block_hi desc)
          (= block_lo block_hi)))

      (var width = (: 32 i64))
      (while (< width (- hi lo))
        (block
          (var left = lo)
          (while (< left hi)
            (block
              (var mid = (+ left width))
              (var right = (+ mid width))
              (if (> mid hi)
                (block (= mid hi)))
              (if (> right hi)
                (block (= right hi)))
              (var i = left)
              (var j = mid)
              (var k = left)
              (while (&& (< i mid) (< j right))
                (block
                  (if (call sr_pair_keep_order store column_idx pairs i j desc)
                    (block
                      (= work [(* k (: 2 i64))] (index pairs (* i (: 2 i64))))
                      (= work [(+ (* k (: 2 i64)) (: 1 i64))]
                         (index pairs (+ (* i (: 2 i64)) (: 1 i64))))
                      (= i (+ i (: 1 i64))))
                    (block
                      (= work [(* k (: 2 i64))] (index pairs (* j (: 2 i64))))
                      (= work [(+ (* k (: 2 i64)) (: 1 i64))]
                         (index pairs (+ (* j (: 2 i64)) (: 1 i64))))
                      (= j (+ j (: 1 i64)))))
                  (= k (+ k (: 1 i64)))))
              (while (< i mid)
                (block
                  (= work [(* k (: 2 i64))] (index pairs (* i (: 2 i64))))
                  (= work [(+ (* k (: 2 i64)) (: 1 i64))]
                     (index pairs (+ (* i (: 2 i64)) (: 1 i64))))
                  (= i (+ i (: 1 i64)))
                  (= k (+ k (: 1 i64)))))
              (while (< j right)
                (block
                  (= work [(* k (: 2 i64))] (index pairs (* j (: 2 i64))))
                  (= work [(+ (* k (: 2 i64)) (: 1 i64))]
                     (index pairs (+ (* j (: 2 i64)) (: 1 i64))))
                  (= j (+ j (: 1 i64)))
                  (= k (+ k (: 1 i64)))))
              (var copy = (* left (: 2 i64)))
              (while (< copy (* right (: 2 i64)))
                (block
                  (= pairs [copy] (index work copy))
                  (= copy (+ copy (: 1 i64)))))
              (= left (+ left (* width (: 2 i64))))))
          (= width (* width (: 2 i64)))))))

  ;; Tie-break every run of equal prefixes in pairs[lo, hi).
  (fun sr_pair_tiebreak_runs
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var pairs <ptr u64>)
        (var work <ptr u64>)
        (var lo i64)
        (var hi i64)
        (var desc bool))
    (block
      (var run_start = lo)
      (var i = (+ lo (: 1 i64)))
      (while (<= i hi)
        (block
          (var boundary = (== i hi))
          (if (! boundary)
            (block
              (= boundary (!= (index pairs (* run_start (: 2 i64)))
                              (index pairs (* i (: 2 i64)))))))
          (if boundary
            (block
              (if (> (- i run_start) (: 1 i64))
                (block
                  (call sr_pair_merge_range store column_idx
                    pairs work run_start i desc)))
              (= run_start i)))
          (= i (+ i (: 1 i64)))))))

  ;; Entry: stable sort of row_ids[0, n) by one string column. `work` must hold
  ;; two pair arrays: 32 * n bytes.
  (fun sort_string_rowids
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var row_ids <ptr i64>)
        (var work <ptr u64>)
        (var counts <ptr u32>)
        (var n i64)
        (var desc bool))
    (block
      (var pairs = work)
      (var pair_work = (cast (+ (cast pairs i64) (* n (: 16 i64))) <ptr u64>))

      (var i i64)
      (= i (: 0 i64))
      (while (< i n)
        (block
          (var row = (index row_ids i))
          (= pairs [(* i (: 2 i64))]
             (call sr_make_prefix store column_idx row))
          (= pairs [(+ (* i (: 2 i64)) (: 1 i64))] (cast row u64))
          (= i (+ i (: 1 i64)))))

      (var digit = (: 0 i64))
      (while (< digit (: 64 i64))
        (block
          (call sr_pair_count_pass pairs pair_work counts n digit desc)
          (= digit (+ digit (: 8 i64)))))

      (call sr_pair_tiebreak_runs store column_idx
        pairs pair_work (: 0 i64) n desc)

      (= i (: 0 i64))
      (while (< i n)
        (block
          (= row_ids [i] (cast (index pairs (+ (* i (: 2 i64)) (: 1 i64))) i64))
          (= i (+ i (: 1 i64)))))))

  ;; Nullable stable counting pass over pairs: bucket 0 or 256 holds nulls per
  ;; nulls_first (mirrors radix_nullable.oz).
  (fun sr_pair_count_pass_nullable
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var pairs <ptr u64>)
        (var work <ptr u64>)
        (var counts <ptr u32>)
        (var n i64)
        (var digit i64)
        (var desc bool)
        (var nulls_first bool))
    (block
      (var i i64)
      (= i (: 0 i64))
      (while (< i (: 257 i64))
        (block
          (= counts [i] (: 0 u32))
          (= i (+ i (: 1 i64)))))

      (= i (: 0 i64))
      (while (< i n)
        (block
          (var row = (cast (index pairs (+ (* i (: 2 i64)) (: 1 i64))) i64))
          (var bucket i64)
          (if (! (call sr_row_valid store row column_idx))
            (block
              (if nulls_first
                (block (= bucket (: 0 i64)))
                (block (= bucket (: 256 i64)))))
            (block
              (= bucket (cast (& (>> (index pairs (* i (: 2 i64)))
                                     (cast digit u64))
                                 (: 255 u64)) i64))
              (if desc
                (block (= bucket (- (: 255 i64) bucket))))
              (if nulls_first
                (block (= bucket (+ bucket (: 1 i64)))))))
          (= counts [bucket] (+ (index counts bucket) (: 1 u32)))
          (= i (+ i (: 1 i64)))))

      (= i (: 1 i64))
      (while (< i (: 257 i64))
        (block
          (= counts [i] (+ (index counts i) (index counts (- i (: 1 i64)))))
          (= i (+ i (: 1 i64)))))

      (= i n)
      (while (> i (: 0 i64))
        (block
          (= i (- i (: 1 i64)))
          (var row = (cast (index pairs (+ (* i (: 2 i64)) (: 1 i64))) i64))
          (var bucket i64)
          (if (! (call sr_row_valid store row column_idx))
            (block
              (if nulls_first
                (block (= bucket (: 0 i64)))
                (block (= bucket (: 256 i64)))))
            (block
              (= bucket (cast (& (>> (index pairs (* i (: 2 i64)))
                                     (cast digit u64))
                                 (: 255 u64)) i64))
              (if desc
                (block (= bucket (- (: 255 i64) bucket))))
              (if nulls_first
                (block (= bucket (+ bucket (: 1 i64)))))))
          (var place = (cast (- (index counts bucket) (: 1 u32)) i64))
          (= work [(* place (: 2 i64))] (index pairs (* i (: 2 i64))))
          (= work [(+ (* place (: 2 i64)) (: 1 i64))]
             (index pairs (+ (* i (: 2 i64)) (: 1 i64))))
          (= counts [bucket] (cast place u32))))

      (= i (: 0 i64))
      (while (< i (* n (: 2 i64)))
        (block
          (= pairs [i] (index work i))
          (= i (+ i (: 1 i64)))))))

  ;; Nullable entry: nulls are grouped at the chosen end by the passes; the
  ;; tie-break runs only over the contiguous valid region.
  (fun sort_string_rowids_nullable
       ((var store <ptr TRowSet>)
        (var column_idx i64)
        (var row_ids <ptr i64>)
        (var work <ptr u64>)
        (var counts <ptr u32>)
        (var n i64)
        (var desc bool)
        (var nulls_first bool))
    (block
      (var pairs = work)
      (var pair_work = (cast (+ (cast pairs i64) (* n (: 16 i64))) <ptr u64>))

      (var i i64)
      (= i (: 0 i64))
      (while (< i n)
        (block
          (var row = (index row_ids i))
          (var prefix = (: 0 u64))
          (if (call sr_row_valid store row column_idx)
            (block
              (= prefix (call sr_make_prefix store column_idx row))))
          (= pairs [(* i (: 2 i64))] prefix)
          (= pairs [(+ (* i (: 2 i64)) (: 1 i64))] (cast row u64))
          (= i (+ i (: 1 i64)))))

      (var digit = (: 0 i64))
      (while (< digit (: 64 i64))
        (block
          (call sr_pair_count_pass_nullable
            store column_idx pairs pair_work counts n digit desc nulls_first)
          (= digit (+ digit (: 8 i64)))))

      (var lo = (: 0 i64))
      (var hi = n)
      (var scanning = #t)
      (while (&& (< lo hi) scanning)
        (block
          (if (! (call sr_row_valid store
                    (cast (index pairs (+ (* lo (: 2 i64)) (: 1 i64))) i64)
                    column_idx))
            (block (= lo (+ lo (: 1 i64))))
            (block (= scanning #f)))))
      (= scanning #t)
      (while (&& (< lo hi) scanning)
        (block
          (if (! (call sr_row_valid store
                    (cast (index pairs (+ (* (- hi (: 1 i64)) (: 2 i64)) (: 1 i64))) i64)
                    column_idx))
            (block (= hi (- hi (: 1 i64))))
            (block (= scanning #f)))))
      (call sr_pair_tiebreak_runs store column_idx
        pairs pair_work lo hi desc)

      (= i (: 0 i64))
      (while (< i n)
        (block
          (= row_ids [i] (cast (index pairs (+ (* i (: 2 i64)) (: 1 i64))) i64))
          (= i (+ i (: 1 i64)))))))
)
